prototype(Carbon.Plausible:Component.Domain) < prototype(Neos.Fusion:Component) {
    value = ${null}
    fallback = ${null}
    siteName = ${null}

    // Internal
    domainValue = ${this.value || this.fallback}
    hasDomainValue = ${this.domainValue && Type.isString(this.domainValue)}
    disableTrackingLink = ${this.hasDomainValue ? '//' + this.domainValue + '/~/disable-tracking' : null}
    embedUrl = ${this.siteName ? '/neos/management/plausible/stats' + (this.siteName == 'default' ? '' : '?' + this.siteName) : null}

    renderer = afx`
        <td>
            <a
                @if.set={props.hasDomainValue}
                class='plausible-domain'
                href={props.embedUrl || 'https://plausible.io/' + props.value}
                target={props.embedUrl ? null : '_blank'}
                rel={props.embedUrl ? null : 'nofollow noopener'}
                title={Translation.translate(props.embedUrl ? 'openEmbedStats' : 'openOnPlausible', 'Open stats from {0}' + (props.embedUrl ? '' : ' on plausible.io'), [props.domainValue], 'Module', 'Carbon.Plausible')}
            >
                {props.domainValue}

                <i class='fas fa-external-link-alt' aria-hidden='true'></i>
            </a>
            {' '}
            <small @if.set={!props.value && props.domainValue}>(Fallback)</small>
            <code @if.set={!props.domainValue}>-</code>
        </td>
        <td>
            <a
                @if.set={props.disableTrackingLink}
                class='plausible-domain'
                href={props.disableTrackingLink}
                target='_blank'
                title={Translation.translate('disableTrackingTitle', 'Disable tracking for {0} in this browser', [props.domainValue], 'Module', 'Carbon.Plausible')}
            >
                {Translation.translate('disableTracking', 'Disable tracking in this browser', [], 'Module', 'Carbon.Plausible')}
                <i class='fas fa-chart-pie' aria-hidden='true'></i>
            </a>
        </td>
    `
}
