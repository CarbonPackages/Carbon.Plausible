prototype(Carbon.Plausible:Component.TableRow) < prototype(Neos.Fusion:Component) {
    key = null
    value = null
    fallback = null
    siteName = null

    // Internal
    value.@process.convertExclusionsArray = ${this.key == 'exclusions' && Type.isArray(value) ? Array.join(value, ',') : value}
    isDomain = ${this.key == 'domain'}
    domainValue = ${this.value || this.fallback}
    hasString = ${this.value && Type.isString(this.value)}
    valueIsBoolean = ${Type.isBoolean(this.value)}

    @if.set = ${this.isDomain || this.valueIsBoolean || this.hasString}

    renderer = afx`
        <tr class={'plausible-row plausible-row--' + String.toLowercase(props.key)}>
            <td>
                {props.key}
            </td>

            <Carbon.Plausible:Component.Domain @if.set={props.isDomain} value={props.value} fallback={props.fallback} siteName={props.siteName} />
            <td @if.set={!props.isDomain && props.valueIsBoolean} colspan='2'>
                <code>{props.value ? 'true' : 'false'}</code>
            </td>
            <td @if.set={!props.isDomain && !props.valueIsBoolean} colspan='2'>
                {props.value}
            </td>
        </tr>
    `
}
