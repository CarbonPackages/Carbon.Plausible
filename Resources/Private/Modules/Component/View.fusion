prototype(Carbon.Plausible:Component.View) < prototype(Neos.Fusion:Component) {
    default = ${this.settings.default}
    screenshot = false
    table = ${['domain', 'host', 'hashBasedRouting', 'exclusions', 'outboundLinks', 'customEvents']}

    renderer = afx`
        <main class='plausible' data-screenshot={props.screenshot}>
            <h1>
                <img width='152' alt='Plausible'>
                    <Neos.Fusion:ResourceUri @path='attributes.src' path='resource://Carbon.Plausible/Public/Logo.png' />
                </img>
            </h1>

            <h2
                @if.set={!props.settings.enable}
                class='plausible-status'
                title={Translation.translate('plausible.status.title', 'Based on the setting Carbon.Plausible.enable', [], 'Module', 'Carbon.Plausible')}
            >
                <i class='fas fa-times' aria-hidden='true' ></i>
                {Translation.translate('plausible.status.disabled', 'Plausible is disabled', [], 'Module', 'Carbon.Plausible')}
            </h2>
            <h2
                @if.set={props.settings.enable}
                class='plausible-status'
                title={Translation.translate('plausible.status.title', 'Based on the setting Carbon.Plausible.enable', [], 'Module', 'Carbon.Plausible')}
            >
                <i class='fas fa-check' aria-hidden='true'></i>
                {Translation.translate('plausible.status.enabled', 'Plausible is enabled', [], 'Module', 'Carbon.Plausible')}
            </h2>

            <p class='plausible-tracking plausible-tracking--disabled'>
                {Translation.translate('trackingStatus.disabled', 'Tracking from {0} is <strong>disabled</strong> in this browser.', [props.requestDomain], 'Module', 'Carbon.Plausible')}
            </p>
            <p class='plausible-tracking plausible-tracking--enabled'>
                {Translation.translate('trackingStatus.enabled', 'Tracking from {0} is <strong>enabled</strong> in this browser.', [props.requestDomain], 'Module', 'Carbon.Plausible')}
            </p>

            <section @if.set={!props.isAdministrator} class='plausible-domainlist'>
                <h2>{Translation.translate('tracking.headline', 'Configured trackings', [], 'Module', 'Carbon.Plausible')}</h2>
                <table class='neos-table'>
                    <tbody>
                        <tr>
                            <Carbon.Plausible:Component.Domain
                                value={props.default.domain}
                                siteName={props.default.sharedLink ? 'default' : null}
                            />
                        </tr>
                        <Neos.Fusion:Loop items={props.settings.sites} @if.set={this.items}>
                            <tr>
                                <Carbon.Plausible:Component.Domain
                                    value={item.domain}
                                    fallback={props.default.domain}
                                    siteName={item.sharedLink ? itemKey : null}
                                />
                            </tr>
                        </Neos.Fusion:Loop>
                    </tbody>
                </table>
            </section>

            <section @if.set={props.isAdministrator}>
                <h2>{Translation.translate('defaultSettings', 'Default settings', [], 'Module', 'Carbon.Plausible')}</h2>
                <table class='neos-table'>
                    <thead>
                        <Carbon.Plausible:Component.TableHeader />
                    </thead>
                    <tbody>
                        <Neos.Fusion:Loop items={props.table}>
                            <Carbon.Plausible:Component.TableRow
                                key={item}
                                value={props.default[item]}
                                siteName={props.default.sharedLink ? 'default' : null}
                            />
                        </Neos.Fusion:Loop>
                    </tbody>
                </table>
                <Carbon.Plausible:Component.Markup defaultSettings={props.settings.default} siteSettings={props.settings.sites} />
            </section>

            <h2 @if.set={props.isAdministrator}>{Translation.translate('siteMappings', 'Site mappings', [], 'Module', 'Carbon.Plausible')}</h2>
            <p @if.set={props.isAdministrator && !props.settings.sites}>
                {Translation.translate('noSiteMappings', 'No specific mapping set.', [], 'Module', 'Carbon.Plausible')}
            </p>
            <Neos.Fusion:Loop items={props.settings.sites} @if.set={props.isAdministrator && this.items}>
                <section>
                    <table class='neos-table'>
                        <thead>
                            <Carbon.Plausible:Component.TableHeader
                                first={Translation.translate('th.rootNodeName', 'Root node name', [], 'Module', 'Carbon.Plausible')}
                                second={itemKey}
                            />
                            <Carbon.Plausible:Component.TableHeader />
                        </thead>
                        <tbody>
                            <Neos.Fusion:Loop items={props.table} itemName='row' itemKey='rowKey'>
                                <Carbon.Plausible:Component.TableRow
                                    key={row}
                                    value={item[row]}
                                    fallback={props.default[row]}
                                    siteName={item.sharedLink ? itemKey : null}
                                />
                            </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                    <Carbon.Plausible:Component.Markup siteName={itemKey} defaultSettings={props.settings.default} siteSettings={props.settings.sites}  />
                </section>
            </Neos.Fusion:Loop>
        </main>

        <aside class='plausible neos-footer' style={props.screenshot ? 'position:static' : null}>
            <a class='neos-button' href='https://plausible.io/docs' target='_blank' rel='nofollow noopener'>
                <i class='fas fa-book' aria-hidden='true'></i> {Translation.translate('button.documentation', 'Open documentation', [], 'Module', 'Carbon.Plausible')}
            </a>
            <button
                type='button'
                onclick={'disableTracking();setStatus()'}
                class='neos-button neos-button-primary plausible-tracking plausible-tracking--enabled'
            >
                <i class='fas fa-times' aria-hidden='true'></i> {Translation.translate('button.dontTrack', "Don't track visits in this browser", [], 'Module', 'Carbon.Plausible')}
            </button>
            <button
                type='button'
                onclick={'enableTracking();setStatus()'}
                class='neos-button plausible-tracking plausible-tracking--disabled'
            >
                <i class='fas fa-check' aria-hidden='true'></i> {Translation.translate('button.doTrack', 'Track visits in this browser', [], 'Module', 'Carbon.Plausible')}
            </button>
        </aside>
    `
}
