prototype(Carbon.Plausible:Component.StringValue) < prototype(Neos.Fusion:Component) {
    label = null
    value = null
    required = false
    fallback = null

    keys = Neos.Fusion:DataStructure {
        false = 'Not set'
        required = 'Not set, but required'
    }

    isValid = ${this.value && Type.isString(this.value)}
    key = ${this.isValid ? 'true' : this.required ? 'required' : 'false'}
    style = ${this.key == 'required' ? 'background:#ff8700' : null}

    renderer = afx`
        <tr>
            <td style={props.style}>{props.label}</td>
            <td style={props.style} @if.set={props.isValid}>
                <a
                    @if.set={props.label == 'domain'}
                    href={'https://plausible.io/' + props.value}
                    target='_blank'
                    rel='nofollow noopener'
                    title={'Open ' + props.value + ' on plausible.io'}
                >
                    {props.value}
                </a>
                {props.label != 'domain' ? props.value : ''}
            </td>
            <td style={props.style} @if.set={!props.isValid}>{props.keys[props.key]}</td>
            <td style={props.style}>{props.isValid ? null : props.fallback}</td>
        </tr>
    `
}


