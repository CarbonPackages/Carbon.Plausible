Carbon.Plausible.PlausibleController {
    index = Neos.Fusion:Component {
        settings = ${Configuration.setting('Carbon.Plausible')}
        default = ${this.settings.default}

        requestMainDomain = Carbon.Plausible:Component.RequestMainDomain
        disableBecauseOfCookie = ${!!request.httpRequest.cookieParams.disabledPlausible}

        table = ${['domain', 'host', 'outboundLinks', 'hashBasedRouting', 'exclusions', 'customEvents']}

        renderer = afx`
            <h1>
                <img width='152' alt='Plausible'>
                    <Neos.Fusion:ResourceUri @path='attributes.src' path='resource://Carbon.Plausible/Public/Logo.png' />
                </img>
            </h1>

            <h2
                @if.set={!props.settings.enable}
                class='plausible-status'
                title={Translation.translate('plausible.status.title', 'Based on the setting Carbon.Plausible.enable', [], 'Module', 'Carbon.Plausible')}
            >
                <i class='fas fa-times' aria-hidden='true' ></i>
                {Translation.translate('plausible.status.disabled', 'Plausible is disabled', [], 'Module', 'Carbon.Plausible')}
            </h2>
            <h2
                @if.set={props.settings.enable}
                class='plausible-status'
                title={Translation.translate('plausible.status.title', 'Based on the setting Carbon.Plausible.enable', [], 'Module', 'Carbon.Plausible')}
            >
                <i class='fas fa-check' aria-hidden='true'></i>
                {Translation.translate('plausible.status.enabled', 'Plausible is enabled', [], 'Module', 'Carbon.Plausible')}
            </h2>

            <p @if.set={props.disableBecauseOfCookie}>
                {Translation.translate('cookieStatus.disabled', 'Tracking from {0} is <strong>disabled</strong> in this browser.', [props.requestMainDomain], 'Module', 'Carbon.Plausible')}
            </p>
            <p @if.set={!props.disableBecauseOfCookie}>
                {Translation.translate('cookieStatus.enabled', 'Tracking from {0} is <strong>enabled</strong> in this browser.', [props.requestMainDomain], 'Module', 'Carbon.Plausible')}
            </p>


            <section>
                <h2>{Translation.translate('defaultSettings', 'Default settings', [], 'Module', 'Carbon.Plausible')}</h2>
                <table class='neos-table'>
                    <thead>
                        <Carbon.Plausible:Component.TableHeader />
                    </thead>
                    <tbody>
                        <Neos.Fusion:Loop items={props.table}>
                            <Carbon.Plausible:Component.TableRow
                                key={item}
                                value={props.default[item]}
                            />
                        </Neos.Fusion:Loop>
                    </tbody>
                </table>
                <Carbon.Plausible:Component.Markup />
            </section>

            <h2>{Translation.translate('siteMappings', 'Site mappings', [], 'Module', 'Carbon.Plausible')}</h2>
            <p @if.set={!props.settings.sites}>
                {Translation.translate('noSiteMappings', 'No specific mapping set.', [], 'Module', 'Carbon.Plausible')}
            </p>
            <Neos.Fusion:Loop items={props.settings.sites} @if.set={this.items}>
                <section>
                    <table class='neos-table'>
                        <thead>
                            <Carbon.Plausible:Component.TableHeader
                                first={Translation.translate('th.rootNodeName', 'Root node name', [], 'Module', 'Carbon.Plausible')}
                                second={itemKey}
                            />
                            <Carbon.Plausible:Component.TableHeader />
                        </thead>
                        <tbody>
                            <Neos.Fusion:Loop items={props.table} itemName='row'>
                                <Carbon.Plausible:Component.TableRow
                                    key={row}
                                    value={item[row]}
                                    fallback={props.default[row]}
                                />
                            </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                    <Carbon.Plausible:Component.Markup siteName={itemKey} />
                </section>
            </Neos.Fusion:Loop>

            <div class='neos-footer'>
                <button
                    type='button'
                    onclick={'setCookie("' + props.requestMainDomain + '")'}
                    class='neos-button neos-button-primary'
                    @if.set={!props.disableBecauseOfCookie}
                >
                    <i class='fas fa-times' aria-hidden='true'></i> {Translation.translate('button.dontTrack', "Don't track visits in this browser", [], 'Module', 'Carbon.Plausible')}
                </button>
                <button
                    type='button'
                    onclick={'deleteCookie("' + props.requestMainDomain + '")'}
                    class='neos-button'
                    @if.set={props.disableBecauseOfCookie}
                >
                    <i class='fas fa-check' aria-hidden='true'></i> {Translation.translate('button.doTrack', 'Track visits in this browser', [], 'Module', 'Carbon.Plausible')}
                </button>
                <a class='neos-button' href='https://plausible.io/docs' target='_blank' rel='nofollow noopener'>
                    <i class='fas fa-book' aria-hidden='true'></i> {Translation.translate('button.documentation', 'Open documentation', [], 'Module', 'Carbon.Plausible')}
                </a>
            </div>
        `
    }
}
