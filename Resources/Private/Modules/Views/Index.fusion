Carbon.Plausible.PlausibleController {
    index = Neos.Fusion:Component {
        settings = ${Configuration.setting('Carbon.Plausible')}
        default = ${this.settings.default}

        requestMainDomain = Carbon.Plausible:Component.RequestMainDomain
        disableBecauseOfCookie = ${!!request.httpRequest.cookieParams.disabledPlausible}

        table = ${['domain', 'host', 'outboundLinks', 'hashBasedRouting', 'customEvents']}

        renderer = afx`
            <h1 style='margin-bottom:20px'>
                <img width='152' alt='Plausible'>
                    <Neos.Fusion:ResourceUri @path='attributes.src' path='resource://Carbon.Plausible/Public/Logo.png' />
                </img>
            </h1>

            <h2 style='font-size:18px;margin:2em 0 1em'>
                <i class='fas fa-check' style='color:#00a338;margin-right:0.5em' @if.set={props.settings.enable}></i>
                <i class='fas fa-times' style='color:#ff8700;margin-right:0.5em' @if.set={!props.settings.enable}></i>
                Plausible is {props.settings.enable ? 'enabled' : 'disabled'}
            </h2>

            <p>
                Tracking from {props.requestMainDomain} is <strong style='font-weight:bold'>{props.disableBecauseOfCookie ? 'disabled' : 'enabled'}</strong> in this browser
            </p>

            <section>
                <h2 style='font-size:18px;margin:2em 0 1em'>Default settings</h2>
                <table class='neos-table'>
                    <thead>
                        <tr>
                            <th width='1%'>Key</th>
                            <th>Value</th>
                        </tr>
                    </thead>
                    <tbody>
                        <Neos.Fusion:Loop items={props.table}>
                            <Carbon.Plausible:Component.Value
                                key={item}
                                value={props.default[item]}
                            />
                        </Neos.Fusion:Loop>
                    </tbody>
                </table>
                <Carbon.Plausible:Component.Markup />
            </section>

            <h2 style='font-size:18px;margin:2em 0 1em'>Site mappings</h2>
            <p @if.set={!props.settings.sites}>No site mapping set.</p>
            <Neos.Fusion:Loop items={props.settings.sites} @if.set={this.items}>
                <section>
                    <table class='neos-table'>
                        <thead>
                            <tr>
                                <th width='1%' style='white-space:nowrap'>Root node name</th>
                                <th>{itemKey}</th>
                            </tr>
                            <tr>
                                <th>Key</th>
                                <th>Value</th>
                            </tr>
                        </thead>
                        <tbody>
                            <Neos.Fusion:Loop items={props.table} itemName='row'>
                                <Carbon.Plausible:Component.Value
                                    key={row}
                                    value={item[row]}
                                    fallback={props.default[row]}
                                />
                            </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                    <Carbon.Plausible:Component.Markup siteName={itemKey} />
                </section>
            </Neos.Fusion:Loop>

            <div class='neos-footer'>
                <button
                    type='button'
                    onclick={'setCookie("' + props.requestMainDomain + '")'}
                    class='neos-button neos-button-primary'
                    @if.set={!props.disableBecauseOfCookie}
                >
                    <i class='fas fa-times'></i> Don't track visits in this browser
                </button>
                <button
                    type='button'
                    onclick={'deleteCookie("' + props.requestMainDomain + '")'}
                    class='neos-button'
                    @if.set={props.disableBecauseOfCookie}
                >
                    <i class='fas fa-check'></i> Track visits in this browser
                </button>
                <a class='neos-button' href='https://plausible.io/docs' target='_blank' rel='nofollow noopener'>
                    <i class='fas fa-book'></i> Open documentation
                </a>
            </div>
        `
    }
}
