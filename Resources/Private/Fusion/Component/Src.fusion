prototype(Carbon.Plausible:Component.Src) < prototype(Neos.Fusion:Component) {
    reverseProxy = null
    host = null
    host.@process.fallback = ${value || 'plausible.io'}
    outboundLinks = null
    hashBasedRouting = null
    exclusions = null
    compat = null
    local = null

    renderer = Neos.Fusion:Join {
        protocol = ${props.reverseProxy ? null : 'https://'}
        host = ${props.reverseProxy ? null : props.host}
        filename = Neos.Fusion:Case {
            useReverseProxy {
                condition = ${props.reverseProxy}
                renderer = '/~plausible'
            }
            ownHost {
                condition = ${props.host != 'plausible.io'}
                renderer = '/js/index'
            }
            default {
                condition = true
                renderer = '/js/plausible'
            }
        }
        hashBasedRouting = ${props.hashBasedRouting ? '.hash' : ''}
        exclusions = ${props.exclusions ? '.exclusions' : ''}
        outboundLinks = ${props.outboundLinks ? '.outbound-links' : ''}
        compat = ${props.compat ? '.compat' : ''}
        local = ${props.local ? '.local' : ''}
        fileextension = '.js'
    }
}
