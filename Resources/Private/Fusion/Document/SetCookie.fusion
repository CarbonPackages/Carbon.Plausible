
prototype(Carbon.Plausible:Document.SetCookie) < prototype(Neos.Fusion:Case) {
    cookieIsAlreadySet {
        condition = ${request.httpRequest.cookieParams.disabledPlausible}
        renderer = Neos.Fusion:Http.Message {
            httpResponseHead {
                statusCode = 307
                headers.Location = '/'
            }
        }
    }
    setCookie {
        condition = true
        renderer = Neos.Fusion:Component {
            domain = Carbon.Plausible:Component.RequestMainDomain
            title = ${Translation.translate('disableCookie', 'Tracking for {0} from this browser is now disabled', [this.domain], null, 'Carbon.Plausible')}
            cookieJS = Neos.Fusion:ResourceUri {
                path = 'resource://Carbon.Plausible/Public/Cookie.js'
            }
            renderer = Neos.Fusion:Http.Message {
                doctype = '<!DOCTYPE html>'
                content = afx`
                    <html>
                        <head>
                            <meta charset='UTF-8' />
                            <title>{props.title}</title>
                            <meta name='robots' content='noindex,nofollow' />
                            <script src={props.cookieJS}></script>
                            <script>{"setCookie();setTimeout(function(){window.location='/'},5000)"}</script>
                        </head>
                        <body style='display:flex;align-items:center;min-height:90vh;'>
                            <h1 style='margin:1em auto;max-width:30ch;text-align:center;font-family:system-ui,-apple-system,Segoe UI,Roboto,Cantarell,Noto Sans,sans-serif;'>{props.title}</h1>
                        </body>
                    </html>
                `
            }
        }
    }

    @cache {
        mode = 'cached'
        entryIdentifier {
            site = ${site}
            plausible = 'CarbonPlausibleSetCookie'
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(site)}
        }
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\PageHandler'
}

root {
    disableTracking {
        @position = 'start'
        condition = ${request.arguments.disablePlausibleTracking == true}
        renderer = Carbon.Plausible:Document.SetCookie
    }
    @cache.entryIdentifier.disablePlausibleTracking = ${request.arguments.disablePlausibleTracking ? 'true' : 'false'}
}
